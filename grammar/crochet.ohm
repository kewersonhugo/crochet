Crochet {
  Program =
    | header Declaration* end

  Declaration =
    | CommandDeclaration
    | DoDeclaration
    | SceneDeclaration
    | ActorDeclaration
    | RelationDeclaration
    
  DoDeclaration =
    | do_ StatementBlock

  SceneDeclaration =
    | scene_ Atom StatementBlock

  ActorDeclaration =
    | actor_ ActorName "::" NonemptyListOf<Atom, ","> ";"   -- roles
    | actor_ ActorName ";"                                  -- no_roles

  RelationDeclaration =
    | relation_ RelationSignature ";"

  RelationSignature =
    | RelationSignatureSegment+

  RelationSignatureSegment =
    | Atom      -- static
    | Name "*"  -- many
    | Name      -- one

  CommandDeclaration =
    | command_ CommandSignature "=" Atom "(" ListOf<Name, ","> ")" ";"  -- ffi
    | command_ CommandSignature StatementBlock                          -- local

  CommandSignature =
    | Name infix_symbol Name           -- infix
    | Name KeywordSignaturePair+       -- self
    | Name Atom                        -- unary
    | KeywordSignaturePair+            -- prefix
    | Atom                             -- nullary
  
  KeywordSignaturePair =
    | Keyword Name

  Statement =
    | ReturnStatement
    | GotoStatement
    | LetStatement
    | FactStatement
    | ForgetStatement
    | Expression ";"   -- expr

  ReturnStatement =
    | return_ Expression ";"  -- with_value
    | return_ ";"             -- naked

  GotoStatement =
    | goto_ Atom ";"

  LetStatement =
    | let_ Name "=" Expression ";"

  FactStatement =
    | fact_ FactUseSignature ";"

  ForgetStatement =
    | forget_ FactUseSignature ";"

  FactUseSignature =
    | FactSegment+

  FactSegment =
    | Atom                -- static
    | PrimaryExpression   -- variable

  StatementBlock =
    | "{" Statement* "}"

  Expression =
    | SearchExpression

  SearchExpression =
    | search_ NonemptyListOf<SearchRelation, ",">    -- search
    | InvokeInfix

  SearchRelation =
    | SearchSegment+

  SearchSegment =
    | ActorName                -- actor
    | Integer                 -- integer
    | Float                   -- float
    | Text                    -- text
    | Boolean                 -- boolean
    | Nothing                 -- nothing
    | Name                    -- variable
    | Atom                    -- static

  InvokeInfix =
    | InvokeMixfix infix_symbol InvokeMixfix  -- infix
    | InvokeMixfix

  InvokeMixfix =
    | InvokePostfix InvokePair+         -- self
    | InvokePair+                       -- prefix
    | InvokePostfix

  InvokePair =
    | Keyword InvokePostfix

  InvokePostfix =
    | InvokePostfix Atom        -- postfix
    | PrimaryExpression

  PrimaryExpression =
    | Text
    | Number
    | Boolean
    | Nothing
    | ActorName           -- actor
    | Atom                -- atom
    | Name                -- variable
    | "(" Expression ")"  -- group

  Text = text
  Number = Integer | Float
  Integer = integer
  Float = float
  Boolean = boolean
  Name = name
  Atom = ~reserved atom
  Keyword = keyword
  ActorName = actor_name
  Nothing = nothing_

  // -- Lexical rules -------------------------------------------------
  header = "%" hs* "crochet" nl
  hs = " " | "\t"
  nl = "\n" | "\r"
  comment = "//" (~nl any)* nl
  spaces += comment

  atom_start = "a".."z"
  atom_rest = letter | digit | "-"
  atom = atom_start atom_rest*

  keyword = atom ":"

  actor_name = "#" atom

  name_start = "A".."Z" | "_"
  name_rest = letter | digit | "-"
  name = name_start name_rest*

  symbol_char = "+" | "/" | "&" | "^" | "~" | "*" | "-" | "<" | ">" | "=" | "."
  infix_symbol = symbol_char+

  text_character =
    | "\\" "\""   -- escape
    | ~"\"" any   -- regular
  text = "\"" text_character* "\""

  dec_digit = "0".."9" | "_"
  integer = ~"_" dec_digit+
  float = ~"_" dec_digit+ "." dec_digit+

  boolean =
    | true_  -- true
    | false_ -- false

  kw<word> = word ~atom_rest

  true_ = kw<"true">
  false_ = kw<"false">
  nothing_ = kw<"nothing">
  scene_ = kw<"scene">
  command_ = kw<"command">
  do_ = kw<"do">
  return_ = kw<"return">
  goto_ = kw<"goto">
  let_ = kw<"let">
  end_ = kw<"end">
  actor_ = kw<"actor">
  relation_ = kw<"relation">
  fact_ = kw<"fact">
  forget_ = kw<"forget">
  search_ = kw<"search">

  reserved =
    | true_ | false_ | nothing_ 
    | scene_ | command_ | do_ | return_ | goto_ | let_ | end_
    | actor_ | relation_ | fact_ | search_ | forget_
}