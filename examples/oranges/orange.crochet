% crochet

actor #lin;
actor #awra;
actor #player;
actor #flag;

relation Who turn;
relation Who player;
relation Who* pronouns are Personal Possessive;
relation Who* name is Name NamePossessive;
relation Who* verb Infinitive Conjugated;

actor #prologue;
actor #main;
relation Chapter chapter;
relation Topic* topic aged No;
relation turn No;

command choose-player = choose-player();
command Actor as-player = get-player(Actor);
command pause = pause();
command show-database = show-database();

command Who she {
  let Actor = Who as-player;
  return ((search X pronouns are Pronoun _ if Actor === X) first)."Pronoun";
}

command Who her {
  let Actor = Who as-player;
  return ((search X pronouns are _ Pronoun if Actor === X) first)."Pronoun";
}

command Who name-is {
  let Actor = Who as-player;
  return ((search X name is _ Name if Actor === X) first)."Name";
}

command Who name {
  let Actor = Who as-player;
  return ((search X name is Name _ if Actor === X) first)."Name";
}

command Verb conjugate: Who {
  return
    match {
      when X player, #player verb Inf Form if Inf === Verb and X === Who {
        return Form;
      }
      else {
        return Verb;
      }
    };
}

command act {
  fact #lin turn;
  trigger action;
  show-database;

  fact #awra turn;
  trigger action;
  show-database;
}

do {
  fact #awra name is "Awra" "Awra's";
  fact #awra pronouns are "she" "her";

  fact #lin name is "Lin" "Lin's";
  fact #lin pronouns are "she" "her";

  fact #player name is "you" "your";
  fact #player pronouns are "you" "your";

  fact #player verb "was" "were";

  "It Tasted Like Oranges..." title show;
  "A short experiment in generative narratives." text show;

  say: "Choose your point of view:";
  let Player = choose-player;
  fact Player turn;
  fact Player player;

  divider show;

  goto prologue;
}

//== Prologue
relation Flag prologue blanket pulled;

scene prologue {
  fact #prologue chapter;

  say:
    "There were a few things that clearly marked the end of the
    long winter season in Ewyg. The noticeably longer days.
    The warming weather. But for those who spent most of their
    time indoors at the Church, these weren't exactly the first
    things that would come to mind.";

  say:
    "When [#awra name] woke [#lin name] up that morning [#awra she] seemed more
    enthusiastic than usual. With a wide smile on [#awra her] face [#awra she]
    had pulled [#lin name-is] blankets while shouting [#lin her] name.
    \"Lin!\"";

  fact #flag prologue blanket pulled;
  act;
  forget #flag prologue blanket pulled;

  say:
    "[#lin name title-case] ["was" conjugate: #lin] awake now.
    And grumpy, yes. But that part was not unusual.";

  say:
    "[#awra name title-case] smiled as [#awra she] watched [#lin name] sit on
    the bed and rub [#lin her] arms to fight the cold. And then [#awra she]
    returned [#lin her] blanket. [#awra name title-case] knew [#lin she]
    wouldn't go back to sleep at that point.";

  act;

  say:
    "\"Why, thank you,\" [#lin name] complained.";

  say:
    "\"I love you too,\" [#awra name] teased [#lin name]. \"And I'd like to ask
    a favour...\"";

  divider show;

  trigger main-transition;
  goto main;
}

action "Go back to sleep"
when
  #lin turn,
  #prologue chapter,
  #flag prologue blanket pulled
{
  say:
    "Still half asleep, [#lin name] tried turning the other way and continue
    [#lin her] forays into dream-land.";

  say:
    "In vain, of course. February mornings were much too chilly in the Church
    dormitory to dare sleep without a blanket.";
}

//== Main
context main-transition {
  when
    #prologue chapter,
    #lin player
  {
    say:
      "A few things had ran through your mind upon hearing that Awra wanted
      you help. But none of them involved... baking sweets.";
  }

  when
    #prologue chapter,
    #awra player
  {
    say:
      "Lin had been taken aback by the request a bit more than you expected.
      You were well aware that Lin was---to put it in friendly terms---not
      the greatest of helpers in the kitchen. But that was fine. You were
      already prepared to handle most of the baking yourself. Still, you
      wanted her to be with you.";

    say:
      "As you processed Lin's reaction, you couldn't help but wonder about
      how bad her self-esteem was. Though it wasn't your primary concern
      at first, if having Lin help you could boost her confidence you would
      be really happy.";
  }
}

scene main {
  fact #main chapter;
  fact turn 0;
  goto main-loop;
}

scene main-loop {
  act;
  trigger next-turn;
  goto main-loop;
}

actor #topic-help;
actor #topic-cookies;
actor #topic-lin-loves-orange-cookies;

action "Ask what you can help with"
when
  #lin turn,
  #main chapter,
  not #topic-help topic aged _
{
  say:
    "<<Is...>>, [#lin name] started saying something, but [#lin her] voice
    trailed off for a moment.";

  say:
    "There was an awkward pause.";

  say:
    "<<Is there anything I can help with?>> [#lin name] managed, at last.";

  fact #topic-help topic aged 0;
}

action "Explain what you're baking"
when
  #awra turn,
  #main chapter,
  not #topic-help topic aged _,
  not #topic-cookies
{
  say:
    "<<I'm glad the head nun allowed me to use the kitchen today.>>
    [#awra name] smiled. <<We'll try baking some orange-flavoured chocolate
    cookies!>>";

  fact #topic-cookies topic aged 0;
}

action "\"I love orange chocolate cookies!\""
when
  #lin turn,
  #main chapter,
  #topic-cookies topic aged Age
  if Age === 0 or Age === 1 or Age === 2
{
  say:
    "<<I love orange chocolate cookies!>> [#lin name] says, effusively.";

  fact #topic-lin-loves-orange-cookies topic aged 0;
}

context next-turn {
  when turn No {
    fact turn (No + 1);
  }

  when turn 100 {
    goto ending;
  }

  when Topic topic aged 10 {
    forget Topic topic aged _;
  }

  when Topic topic aged No {
    fact Topic topic aged (No + 1);
  }
}

scene ending {
  say: "The end...";
}

// Sticky actions
action "(Continue reading...)"
when Anyone turn {}

